@def title = "AoC 2021"
@def pubdate = "2021-1-22"
@def longtitle = "My favourite Advent of Code 2021 Puzzle"
@def tags = ["code", "julia"]

# My favourite Advent of Code 2021 Puzzle
\toc
## What is Advent Of Code?
@@marginnote
Although this is probably not needed for the audience of my blog.
@@
First, maybe a short introduction what [Advent of Code](https://adventofcode.com) even is.
Essentially, it is an advent calendar consisting of 25 coding puzzles.
The cool part is that everyone gets a personalized input every day and has to submit the requested solution for his/her data.\
The puzzles start out quite easy and become more and more challenging over the duration of december.
Additionally each day consists of two parts. The second part gets only revealed after solving the first one.
So, one should be careful going for a greedy first solution.\
Two more short notes, how I solved those puzzles.
@@marginnote
Or any other library that allows python to call C code.
@@
* Since 2020, I used [Julia](https://julialang.org) for all puzzles. Coming from a python background, after a small adaption time it is a joy not having to rely on numpy when coding fast implementations.
* At least the second half of the puzzles, I solved together with my Dad. It made me realize how good pair-programming can be. Actively talking about the design of the solution, as well as having two pair of eyes looking for bugs, led in my experience to a better code style and more correct solutions.

## Day 22 
@@marginnote
You can find the full explanation and examples by clicking on day 22 to the left.
@@
I liked many puzzles last year (2021), but I found [day 22](https://adventofcode.com/2021/day/22) especially pleasing.

### The puzzle
@@marginnote
Voxels are the 3D equivalent of what pixels are for 2D.
@@
The puzzle instructions were a list of 3d cuboids, which turn every voxel inside of them either __on__ or __off__.
An example input could look like the following:
@@marginnote
I constructed this example such that all cuboids are planes in the x-y dimension. Now i can visualize them in 2D and still be correct.
@@
> on x=-8..7,y=-13..-5,z=12..12\
> on x=18..28,y=-7..3,z=-12..12\
> off x=-2..23,y=-10..1,z=-12..12
Visualized, the 2D x-y part of those three steps look like the following:
@@marginnote
For the purpose of simplicity visualizations will only be in 2D, yet we will quickly realize that the solution scales to an arbitrary number of dimensions.\
All white pixels are also off, the red ones are only to indicate that there is an explicit command, to turn them off.
@@
![overlapping on and off](/assets/blogposts/AoC2021/overlap.png)

In the beginning all voxels are off.
The question is then, how many voxels are __on__ after all cuboids.\
In the little toy example above, the answer would be 155.\
The first part of the puzzle was only about the voxels in a certain subspace of $\R^3$, but I will instantly talk about the solution for all voxels.

### My solution
The brute force solution would be to keep track of the status of every voxel and update its status for every cuboid.
This approach quickly breaks down after seeing that the space where one needs to keep track of voxels is at least 100000 in x, y and z.
@@marginnote
The division by 8 is because every voxel only needs to store 1 Bit (on / off) and the right side is in Byte
@@
This corresponds to $\frac{100000^3}{8}=125TB$ of memory needed.

The first simplification is that we only need to keep track of the voxels that are on.
And we can even optimize further by always saving entire regions / cuboids that are on instead of single voxels.
Let us first define a few structures and type aliases which we need:
```julia
Cuboid = Tuple{UnitRange{Int},UnitRange{Int},UnitRange{Int}}
NonOLCuboids = Vector{Cuboid} # stands for Non-overlapping

# every input line was called a command,
# consisting of the cuboid itself and the information whether it is on or off
struct Command
    cube::Cuboid
    turn_on::Bool
end
```
This implementation of a cuboid as a Tuple of `UnitRanges` and the respective structure for non overlapping cuboids allow us to directly calculate their volume:
```julia
volume(c::Cuboid) = prod(length.(c))
volume(nolc::NonOLCuboids) = sum(volume.(nolc))
```
The next logical question is, how do we handle cuboid intersections?\
First, we need to calculate the intersection volume of two cuboids.
Important to note is that an intersection of two cuboids is again a cuboid.
With our representation of cuboids, the function for calculating them is pretty straight forward.
@@marginnote
The function is called Base.intersect to ensure that we do not hide the intersect function defined in julia's Base.
@@
```julia
function Base.intersect(c1::Cuboid, c2::Cuboid) 
    t = (intersect(c1[i],c2[i]) for i in 1:3)
    any(r -> length(r)==0, t) && return nothing
    Cuboid(t)
end
```
The idea, how to handle intersections is the following: If a new cuboid intersects with an existing one, we keep the new one and remove the intersection out of the old one.
Since we only store cuboids, we need to split the existing one into mulitple other ones.
The split we want to achieve looks like this:

![split example](/assets/blogposts/AoC2021/split.png)

The 5 cuboids look a little bit redundant? Why don't we merge cuboid 1, 2 and 3, or cuboid 1 and 4?
The answer is that this split is easy to compute for an arbitrary intersect and even for any number of dimensions.
To achieve this split, we compute the following segments for each dimension. 
* The intersect in the respective dimension; [$\text{inter}_\text{start} : \text{inter}_\text{end}$]
* If the start of the old cuboid is more to the left, than the intersect, then we add the segment between the start of the old cuboid and the intersect; if $\text{cub}_\text{start} < \text{inter}_\text{start}$, add [$\text{cub}_\text{start} : \text{inter}_\text{start}$]
* If the end of the old cuboid is more to the right, than the intersect, then we add the segment between the end of the intersect and the old cuboid; if $\text{cub}_\text{end} > \text{inter}_\text{end}$, add [$\text{inter}_\text{end} : \text{cub}_\text{end}$]
The desribed segments for the example above are:
> x: 1:2, 3:13, 14:16\
> y: 1:2, 3:9
@@marginnote
The cartesian product of two sets contains all unique combinations of two elements.
In this case, they are [(1:2, 1:2), (3:13, 1:2), (14:16, 1:2), (1:2, 3:9), (3:13, 3:9), (14:16, 3:9)]
@@
The splitted cuboids are the cartesian product of those segments of all (three) dimensions, excluded the one where all segments are the intersect segment.\
In Julia an implementation for this split might look like this:
```julia
struct Section
    segment::UnitRange{Int}
    is_intersect::Bool
end

function split_dimension(old_section::UnitRange{Int}, new_section::UnitRange{Int})
    splitted = [Section(new_section,true)]
    if new_section[1] > old_section[1]
        push!(splitted, Section(old_section[1]:new_section[1]-1,false))
    end
    if new_section[end] < old_section[end]
        push!(splitted, Section(new_section[end]+1:old_section[end],false))
    end
    splitted
end

function split_cuboid(c::Cuboid, intersect::Cuboid)
    dimensions_splits = Tuple(split_dimension(c[i],intersect[i]) for i in 1:3)
    splitted_cuboids = NonOLCuboids()
    for x in dimensions_splits[1], y in dimensions_splits[2], z in dimensions_splits[3]
        all(i->i.is_intersect, (x,y,z)) && continue
        push!(splitted_cuboids, Cuboid((x.segment,y.segment,z.segment)))
    end
    splitted_cuboids
end
```
The last step needed for the task is to iterate over all commands and in every iteration build up a new list of non overlapping cuboids.
For each command, we iterate over all the non overlapping cuboids of the previous iteration and do:
* Keep it, if it has no intersect if the new one
* Split it and add all its splitted cuboids to the list, if it has an intersect with the new one
Last but not least, the new cuboid only gets added to the list, if it is **on** cuboid (otherwise we do not need to save it).\
My function for this last part of the algorithm looks like this:
```julia
function execute_commands(commands)
    on_cuboids = NonOLCuboids()
    for c in commands
        next_on_cuboids = NonOLCuboids()
        for on_c in on_cuboids
            inter = intersect(c.cube, on_c)
            if !isnothing(inter)
                push!(next_on_cuboids, split_cuboid(on_c, inter)...)
            else
                push!(next_on_cuboids, on_c)
            end
        end
        if c.turn_on
            push!(next_on_cuboids, c.cube)
        end
        on_cuboids = next_on_cuboids
    end
    volume(on_cuboids)
end
```

The full code can be found on my [github repository](https://github.com/Tobias-Thomas/AdventOfCode/).
